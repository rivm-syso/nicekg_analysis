PREFIX nevonut: <https://data.rivm.nl/data/nvip/bigfood/nevo/nutrient#>
PREFIX fobi: <http://purl.obolibrary.org/obo/FOBI>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX leda: <http://example.org/temporary_identifier/LEDA#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX fio: <http://www.foodvoc.org/resource/fio#>
PREFIX om: <http://www.ontology-of-units-of-measure.org/resource/om-2/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX foodon: <http://purl.obolibrary.org/obo/FOODON>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX kap: <http://example.org/>
PREFIX saref: <https://saref.etsi.org/core/>

SELECT DISTINCT 
       ?foodname 
       ?proteinvalue 
       ?nutunit 
       ?impactfoodname 
       ?roundedWarmingValue 
       ?impactunit 
       ?impactRoundProteinRatio
WHERE {
  # Subquery 1: Retrieve nutritional (protein) data
  {
    SELECT ?foodname ?proteinvalue ?nutunit ?foodontype WHERE {
      {
        ?foodontype rdfs:subClassOf* foodon:_00001015 . # Plant food product 
        BIND("Plant food product" AS ?grouplabel)
      }
      FILTER NOT EXISTS {
        ?foodontype rdfs:subClassOf* foodon:_00001172 . # Exclude nut food product
      }

      # Retrieve NEVO nutritional data
   ?food_item rdf:type fio:FoodItem . 
      ?food_item rdf:type ?foodontype . 
      ?food_item skos:prefLabel ?foodname . 
      FILTER(LANG(?foodname) = "en")
      ?food_item fio:hasNutritionalValue ?nutvalue . 
      ?nutvalue fio:hasNutrient nevonut:PROT .
      ?nutvalue fio:hasNutrient ?nutclass . 

      ?nutclass rdfs:label ?nutrientclasslabel . 
      ?nutvalue om:hasValue ?valuelink .    
      ?valuelink om:hasNumericalValue ?proteinvalue . 
      FILTER(?proteinvalue >= 12) 
      ?valuelink om:hasUnit ?nutunit .
      FILTER(isLITERAL(?nutunit)) 
    }
  }

  # Subquery 2: Retrieve environmental impact (global warming potential)
  {
    SELECT ?impactfoodname ?warmingvalue ?roundedWarmingValue ?impactunit ?foodontype WHERE {
      {
        ?foodontype rdfs:subClassOf* foodon:_00001015 .
        BIND("Plant food product" AS ?grouplabel)
      }
      FILTER NOT EXISTS {
        ?foodontype rdfs:subClassOf* foodon:_00001172 . # Exclude nut food product
      }

      # Retrieve environmental impact data
      ?food_item rdf:type fio:FoodItem . 
      ?food_item rdf:type ?foodontype . 
      ?food_item rdfs:label ?impactfoodname . 
      ?food_item fio:hasImpact ?impact . 
      FILTER(LANG(?impactfoodname) = "en") 
      ?impact om:hasValue ?impactvalue . 
      ?impact rdfs:label "Global warming"@en .
      ?impactvalue om:hasNumericalValue ?warmingvalue . 
      BIND(ROUND(?warmingvalue * 100) / 100 AS ?roundedWarmingValue)
      ?impactvalue om:hasUnit ?impactunit .
      FILTER(isLITERAL(?impactunit))
    }
  }

  # Calculate the impact protein ratio
  BIND(?warmingvalue / ?proteinvalue AS ?impactProteinRatio)
  BIND(ROUND(?impactProteinRatio * 100) / 100 AS ?impactRoundProteinRatio)
}
ORDER BY ASC(?impactRoundProteinRatio) DESC(?foodname)