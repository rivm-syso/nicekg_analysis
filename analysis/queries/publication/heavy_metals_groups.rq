PREFIX kap_param: <https://data.rivm.nl/data/nvip/bigfood/kap/parameter#>
PREFIX om: <http://www.ontology-of-units-of-measure.org/resource/om-2/>
PREFIX saref: <https://saref.etsi.org/core/>
PREFIX fio: <http://www.foodvoc.org/resource/fio#>
PREFIX kap: <http://example.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foodon: <http://purl.obolibrary.org/obo/FOODON>

select distinct ?grouplabel ?foodname ?propertylabel ?value ?unitlabel where {

       {
        ?foodontype rdfs:subClassOf* foodon:_00001264 .
        BIND("Legume food product" AS ?grouplabel)
    }
    UNION
    {
        ?foodontype rdfs:subClassOf* foodon:_00001248 .
        BIND("Fish food product" AS ?grouplabel)
    }
    UNION
    {
        ?foodontype rdfs:subClassOf* foodon:_00001006 .
        BIND("Mamallian meat food product" AS ?grouplabel)
    }
    UNION
    {
        ?foodontype rdfs:subClassOf* foodon:_00001057.
        BIND("Plant fruit food product" AS ?grouplabel)
    }
    
    ?kap rdf:type sosa:FeatureOfInterest . 
    ?kap rdf:type ?foodontype . 
    ?kap kap_param:hasKapName ?foodname  . 
    ?kap saref:hasMeasurement ?measurement .
    ?measurement saref:relatesToProperty ?property . 
    ?property kap_param:hasKapName ?propertylabel.
    ?measurement om:hasNumericalValue ?value .
    ?measurement saref:isMeasuredIn ?unit . 
    ?unit om:hasUnit ?unitlabel . 
    FILTER(isLiteral(?unitlabel)) 
    ?foodontype rdfs:label ?foodonlabel . 
    filter(lang(?foodname) = "en") . 
    filter(lang(?propertylabel) = "en")
    VALUES ?propertylabel {
        "MERCURY (TOTAL)"@en 
        "LEAD"@en
        "NICKEL"@en
		"CADMIUM"@en	
		"ARSENIC"@en
    }
} 